Dataset ecommerce_vux {
  __engine__: 'aql'
  label: 'Ecommerce Vux'
  description: "Dataset for Dynamic Content demo"
  data_source_name: "demodb"
  models: [
    orders,
    users,
    sales_model,
    order_items,
    countries,
    merchants,
    products,
    cities,
    param_model
    ,
    user_lookup_csv
    ,
    ecommerce_financial
    ,
    categories
  ]

  relationships: [
    relationship(orders.user_id > users.id, true),
    relationship(order_items.order_id > orders.id, true),
    relationship(cities.country_code > countries.code, true),
    relationship(users.city_id > cities.id, true),
    relationship(order_items.product_id > products.id, true),
    relationship(products.merchant_id > merchants.id, true),
    relationship(merchants.city_id > cities.id, false),
    relationship(order_items.order_id - sales_model.order_id, true)
    ,
    relationship(user_lookup_csv.user_id - users.id, true)
  ]

  owner: 'khai.to+demo4@holistics.io'

  metric revenue {
    label: 'Revenue'
    type: 'number'
    definition: @aql sum(order_items, order_items.quantity * products.price) ;;
  }

  metric total_orders {
    label: 'Total Orders'
    type: 'number'
    definition: @aql count(orders.id) ;;
  }

  metric total_users {
    label: 'Total Users'
    type: 'number'
    definition: @aql count(users.id) ;;
  }

  metric dynamic_metric {
    label: 'Dynamic Metric'
    type: 'number'
    definition: @aql
      case(
        when: (param_model.metric_selection | first()) == 'Revenue'
        , then: revenue
        , when: (param_model.metric_selection | first()) == 'Total Orders'
        , then: total_orders
        , when: (param_model.metric_selection | first()) == 'Total Users'
        , then: total_users
      )
     ;;
  }

  dimension dynamic_dim {
    model: users
    label: 'Dynamic Breakdown Dimension'
    type: 'text'
    definition: @aql case(
      when: 'Countries' in param_model.dim_choice
      , then: countries.name
      , when: 'Cities' in param_model.dim_choice
      , then: cities.name
      , when: 'Gender' in param_model.dim_choice
      , then: users.gender
    ) ;;
  }

  dimension now_dim {
    model: orders
    label: 'Now Dim'
    type: 'datetime'
    definition: @aql @(now) ;;
  }
}