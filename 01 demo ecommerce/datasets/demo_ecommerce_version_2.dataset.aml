const orders_detail = [
  r(ecommerce_orders.id),
  r(ecommerce_orders.status),
  r(ecommerce_orders.created_at),
  r(ecommerce_orders.discount),
  r(ecommerce_orders.delivery_attempts),
]

const users_detail = [
  r(ecommerce_users.id),
  r(ecommerce_users.full_name),
  r(ecommerce_users.gender),
  r(ecommerce_users.email),
  r(ecommerce_cities.name),
]

const products_detail = [
  r(ecommerce_products.id),
  r(ecommerce_products.name),
  r(ecommerce_products.price),
]

@tag('1️⃣ Trust/Endorsed')
Dataset demo_ecommerce_version_2 {
  label: '[Demo] Ecommerce Official (2025)'
  description: '''
# Dataset Capabilities

## What this dataset can answer
- Financials: Waterfall from GMV (Demand) to NMV (Fulfilled Sales) to Revenue (Platform Earnings).
- Operational Health: Distinctions between pre-delivery failures (cancellations) and post-delivery failures (refunds).
- Customer Loyalty: Identification of repeat buyers (is_repeated_buyer).
- Granularity: Performance by Merchant, Product Category, Country, and City.

## What this dataset cannot answer
- Net Profit: Operational costs (marketing, server costs, salaries) are not in this dataset.
- Marketing Attribution: Traffic sources (e.g., Google Ads vs. Facebook) are not tracked here.
- Inventory: There is no stock level or "out of stock" data.

# Persona Guidelines
- Executives:
  - Focus on high-level metrics, trends, Revenue, YoY.
- Sales / Category Manager
  - Focus on merchant/category rankings.
- Data Team
  - Be precise about definitions and grain.
'''
  data_source_name: 'demodb'
  owner: 'vincent@holistics.io'

  models: [
    ecommerce_products,
    ecommerce_merchants,
    ecommerce_countries,
    ecommerce_orders,
    ecommerce_users,
    ecommerce_order_items,
    ecommerce_cities,
    map_categories,
    param_model_demo,
    dim_dates,
    ecommerce_employees,
    date_period,
  // ecommerce_product_images,
  ]
  relationships: [
    relationship(ecommerce_order_items.order_id > ecommerce_orders.id, true),
    relationship(ecommerce_orders.user_id > ecommerce_users.id, true),

    relationship(ecommerce_users.city_id > ecommerce_cities.id, true),
    relationship(ecommerce_cities.country_code > ecommerce_countries.code, true),
    relationship(ecommerce_merchants.city_id > ecommerce_cities.id, false), // optional: not all analyses need this

    relationship(ecommerce_order_items.product_id > ecommerce_products.id, true),
    relationship(ecommerce_products.merchant_id > ecommerce_merchants.id, true),
    relationship(ecommerce_products.category_id > map_categories.category_id, true),

    relationship(ecommerce_orders.created_date > dim_dates.date_key, true),
    relationship(ecommerce_users.sign_up_date > dim_dates.date_key, false), // optional: secondary date relationship
    relationship(ecommerce_employees.merchant_id > ecommerce_merchants.id, true),

  // ═══════════════════════════════════════════════════════════════════════════
  // DISABLED RELATIONSHIPS
  // ═══════════════════════════════════════════════════════════════════════════
  // relationship(ecommerce_products.id - ecommerce_product_images.product_id, true),

  ]

  // ═══════════════════════════════════════════════════════════════════════════
  // PERMISSIONS
  // ═══════════════════════════════════════════════════════════════════════════

  permission vendor_access {
    field: ref('ecommerce_merchants', 'id')
    operator: 'matches_user_attribute'
    value: 'vendor_id'
  }

  permission country_access {
    field: ref('ecommerce_countries', 'code')
    operator: 'matches_user_attribute'
    value: 'country'
  }

  permission city_access {
    field: ref('ecommerce_cities', 'name')
    operator: 'matches_user_attribute'
    value: 'city'
  }

  // permission pii_access {
  //   field: ref('ecommerce_users', 'email_pii')
  //   operator: 'matches_user_attribute'
  //   value: 'pii_access'
  // }

  // ═══════════════════════════════════════════════════════════════════════════
  // DIMENSIONS
  // ═══════════════════════════════════════════════════════════════════════════

  dimension cohort_month {
    model: ecommerce_users
    label: "Cohort Month"
    type: 'date'
    definition: @aql min(ecommerce_orders.created_at | month()) | dimensionalize(ecommerce_users.id);;
  }

  dimension month_number {
    model: ecommerce_orders
    label: 'Month Number'
    type: 'number'
    definition: @aql date_diff('month', ecommerce_users.cohort_month, (ecommerce_orders.created_at | month())) ;;
  }

  dimension cohort_size {
    model: ecommerce_orders
    label: 'Cohort Size'
    type: 'number'
    definition: @aql count_distinct(ecommerce_orders.user_id) | exact_grains(ecommerce_users.cohort_month) ;;
  }

  dimension total_orders_all_time {
    model: ecommerce_users
    label: 'Total Orders (Users)'
    type: 'number'
    definition: @aql total_orders | dimensionalize(ecommerce_users.id);;
  }

  dimension is_buyer {
    model: ecommerce_users
    label: 'Is Buyer'
    type: 'truefalse'
    definition: @aql ecommerce_users.total_orders_all_time > 0;;
  }

  dimension is_repeated_buyer {
    model: ecommerce_users
    label: 'Is Repeated Buyer'
    type: 'truefalse'
    definition: @aql ecommerce_users.total_orders_all_time > 1;;
  }

  dimension product_performance_analysis {
    model: ecommerce_products
    label: 'Product Performance Analysis'
    type: 'text'
    definition: @aql concat(
      'https://claude.ai/new?q=write+an_analysis+for+',
      ecommerce_products.name,
      '.+The+GMV+for+this+product+is+',
      cast(gmv, 'text'),
      '+and+the+AOV+for+this+product+is+',
      cast(aov, 'text'),
      )
     ;;
  }

  dimension product_image {
    model: ecommerce_product_images
    label: 'Product Image'
    type: 'text'
    definition: @aql concat('<img src="', ecommerce_product_images.url, '" style="text-align:center;height:50px;object-fit:cover" />') ;;
  }

  dimension breakdown_dim {
    label: 'Dynamic Breakdown Dimension'
    type: 'text'
    model: ecommerce_orders
    definition: @aql case(
        when: 'Country Names' in param_model_demo.dim_selections
        , then: ecommerce_countries.name

        , when: 'Age Demographic' in param_model_demo.dim_selections
        , then: ecommerce_users.age_group

        , when: 'Order Status' in param_model_demo.dim_selections
        , then: ecommerce_orders.status

        , else: ecommerce_countries.name
      ) ;;
    description: 'The dimension will be automatically switched among Country, Age Group, Order Status. By default, it will be Country'
  }

  dimension product_count_within_1_month {
    model: map_categories
    label: 'Product Count (Within 1 Month)'
    type: 'number'
    definition: @aql
      dimensionalize(
        ecommerce_order_items
          | filter(ecommerce_orders.created_at matches @(last 1 month))
          | count(ecommerce_products.id)
        , map_categories.category_id
      )
    ;;
  }

  dimension active_category {
    model: map_categories
    label: 'Active Category'
    type: 'text'
    definition: @aql
      case(
        when: map_categories.product_count_within_1_month > 150, then: map_categories.category,
        else: null
      )
    ;;
  }

  // ═══════════════════════════════════════════════════════════════════════════
  // METRICS
  // ═══════════════════════════════════════════════════════════════════════════

  metric total_users {
    label: 'Total Users'
    type: 'number'
    definition: @aql count(ecommerce_users.id) ;;
    description: ''
  }

  metric total_orders {
    label: "Total Orders"
    type: "number"
    hidden: false
    definition: @aql count(ecommerce_orders.id);;
    description: ''
  }

  metric gmv {
    label: "GMV (Gross Merchandise Value)"
    type: "number"
    hidden: false
    definition: @aql ecommerce_order_items | sum(ecommerce_order_items.quantity * ecommerce_products.price);;
    format: "[\$\$]#,###0"
    description: 'Gross value of items ordered before discounts; includes all statuses.'
  }

  metric total_discount {
    label: "Total Discount"
    type: "number"
    hidden: false
    definition: @aql gmv - revenue;;
    format: "[\$\$]#,###0"
    description: '''GMV-to-Revenue gap.
Note: This is not a pure discount measure; it blends discount effects, cancellations/refunds (via NMV), and commission mechanics. Do not present it as "discount given".
'''
  }

  metric nmv {
    label: "NMV (Net Merchandise Value)"
    type: "number"
    hidden: false
    definition: @aql (ecommerce_order_items | sum(ecommerce_order_items.quantity * ecommerce_products.price * (1 - ecommerce_orders.discount))) |  where(ecommerce_orders.status not in ['cancelled', 'refunded']);;
    format: "[\$\$]#,###0"
    description: 'Realized fulfilled value after order-level discounts; excludes `cancelled` and `refunded`.'
  }

  metric revenue {
    label: "Total Revenue"
    type: "number"
    hidden: false
    definition: @aql nmv * revenue_commission;;
    format: "[\$\$]#,###0"
    description: '''Platform revenue from fulfilled orders.
Note: Not net profit; commission is demo-fixed (see `revenue_commission`).
'''
  }

  metric aov {
    label: "AOV (Average Order Value)"
    type: "number"
    hidden: false
    definition: @aql gmv / total_orders;;
    format: "[\$\$]#,###0"
    description: '''Average gross value per order.
Note: For "fulfilled AOV," you would need NMV per delivered order.
'''
  }

  metric total_orders_across_all {
    label: "Total Orders Across All"
    type: "number"
    hidden: false
    definition: @aql total_orders | of_all(ecommerce_orders);;
    description: ''
  }

  metric percent_of_total {
    label: "Percent Of Total Orders"
    type: "number"
    hidden: false
    definition: @aql total_orders * 1.0 / total_orders_across_all;;
    format: "#,###0.00%"
    description: ''
  }

  metric total_delivered_orders {
    label: "Total Delivered Orders"
    type: "number"
    hidden: false
    definition: @aql total_orders | where(ecommerce_orders.status is 'delivered');;
    description: 'Count of orders with status `delivered`.'
  }

  metric total_refunded_orders {
    label: "Total Refunded Orders"
    type: "number"
    hidden: false
    definition: @aql total_orders | where(ecommerce_orders.status is 'refunded');;
    description: 'Count of orders with status `refunded`.'
  }

  metric total_cancelled_orders {
    label: "Total Cancelled Orders"
    type: "number"
    hidden: false
    definition: @aql total_orders | where(ecommerce_orders.status is 'cancelled');;
    description: 'Count of orders with status `cancelled`.'
  }

  metric cancelled_order_ratio {
    label: "Cancelled Order Ratio"
    type: "number"
    hidden: false
    definition: @aql total_cancelled_orders / total_orders;;
    format: "#,###0.00%"
    description: '''Cancellation rate by count.
Note: Sensitive to how `total_orders` includes all statuses; compare consistently across segments/time.
'''
  }

  metric cancelled_value {
    label: "Cancelled Value"
    type: "number"
    hidden: false
    definition: @aql gmv | where(ecommerce_orders.status is 'cancelled');;
    format: "[\$\$]#,###0"
    description: 'GMV of cancelled orders.'
  }

  metric cancelled_value_ratio {
    label: "Cancelled Value Ratio"
    type: "number"
    hidden: false
    definition: @aql cancelled_value / gmv;;
    format: "#,###0.00%"
    description: 'Share of GMV lost to cancellations.'
  }

  metric delivered_value {
    label: "Delivered Value"
    type: "number"
    hidden: false
    definition: @aql gmv | where(ecommerce_orders.status is 'delivered');;
    format: "[\$\$]#,###0"
    description: ''
  }

  metric refunded_value {
    label: "Refunded Value"
    type: "number"
    hidden: false
    definition: @aql gmv | where(ecommerce_orders.status is 'refunded');;
    format: "[\$\$]#,###0"
    description: ''
  }

  metric retention {
    label: 'Cohort Retention'
    type: 'number'
    hidden: false
    definition: @aql (total_users * 1.0) / (total_users | of_all(ecommerce_orders.month_number)) ;;
    format: "#,###0.00%"
    description: 'Cohort retention rate (users in a cohort active in month N / cohort size).'
  }

  metric total_buyers {
    label: "Total Buyers"
    type: "number"
    hidden: false
    definition: @aql count(ecommerce_users.id) | where(ecommerce_users.is_buyer is true);;
    description: 'Count of users with at least one order, where `is_buyer = total_orders_all_time > 0`.'
  }

  metric total_repeated_buyers {
    label: "Total Repeated Buyers"
    type: "number"
    hidden: false
    definition: @aql count(ecommerce_users.id) | where(ecommerce_users.is_repeated_buyer is true);;
    description: '''Count of users with 2+ orders (`is_repeated_buyer = total_orders_all_time > 1`).
Note: this can pair with `total_buyers` or `total_users` for a ratio.
'''
  }

  metric running_total_orders {
    label: "Running Total Orders"
    type: "number"
    hidden: false
    definition: @aql running_total(total_orders, ecommerce_orders.created_at | year());;
    description: 'Cumulative orders over time (running total by year).'
  }

  metric dynamic_metric {
    label: 'Dynamic Metric (no format)'
    type: 'number'
    definition: @aql
      case(
        
        when: 'total users' in param_model_demo.metric_selections
        , then: ecommerce_users.total_users

        , when: 'total orders' in param_model_demo.metric_selections
        , then: total_orders

        , when: 'platform revenue' in param_model_demo.metric_selections
        , then: revenue

        , when: 'GMV' in param_model_demo.metric_selections
        , then: gmv

        , when: 'NMV' in param_model_demo.metric_selections
        , then: nmv

        , when: 'AOV' in param_model_demo.metric_selections
        , then: aov

      )
     ;;
    description: ''
  }

  metric dynamic_metric_currency {
    label: 'Dynamic Metric (Currency)'
    type: 'number'
    definition: @aql
      case(
        when: 'platform revenue' in param_model_demo.metric_selections_currency
        , then: revenue

        , when: 'GMV' in param_model_demo.metric_selections_currency
        , then: gmv

        , when: 'NMV' in param_model_demo.metric_selections_currency
        , then: nmv

        , when: 'AOV' in param_model_demo.metric_selections_currency
        , then: aov

      )
     ;;
    format: "[\$\$]#,###0"
    description: ''
  }

  metric revenue_commission {
    label: "Revenue Commission Rate (Demo from May 2025)"
    type: "number"
    hidden: false
    definition: @aql 0.5;;
    format: "#,###%"
    description: 'Fixed demo commission rate.'
  }
  metric metric_a1cdc7b {
    label: "Classification of Total Users"
    type: "number"
    hidden: false
    description: ""
    definition: @aql case(
  when: thong_test.period == 'Week',
  then: total_buyers,
  else: total_users
);;
  }
  metric metric_67076ce {
    label: "PoP Comparison of Total Orders"
    type: "number"
    hidden: false
    description: ""
    definition: @aql total_orders | relative_period(ecommerce_products.created_at, interval(-1 year), strict: true);;
    format: ""
  }

  // ═══════════════════════════════════════════════════════════════════════════
  // CUSTOM VIEWS
  // ═══════════════════════════════════════════════════════════════════════════

  view {
    // Core Volume Metrics
    group volume_metrics {
      metric total_orders
      metric total_delivered_orders
      metric total_cancelled_orders
      metric total_refunded_orders
      metric running_total_orders
      metric total_orders_across_all
    }

    // User & Customer Metrics
    group user_metrics {
      metric total_users
      metric total_buyers
      metric total_repeated_buyers
      metric retention
    }

    // Revenue & Value Metrics
    group revenue_metrics {
      metric gmv
      metric nmv
      metric revenue
      metric aov
      metric total_discount
      metric revenue_commission
    }

    // Order Value by Status
    group order_value_metrics {
      metric delivered_value
      metric cancelled_value
      metric refunded_value
    }

    // Performance & Ratio Metrics
    group performance_metrics {
      metric percent_of_total
      metric cancelled_order_ratio
      metric cancelled_value_ratio
    }

    model ecommerce_products {
    }
    model ecommerce_merchants {
    }
    model ecommerce_countries {
    }
    model ecommerce_orders {
    }
    model ecommerce_users {
    }
    model ecommerce_order_items {
    }
    model ecommerce_cities {
    }
    model map_categories {
    }
    model dim_dates {
    }
    model ecommerce_employees {
    }
  }

  // ═══════════════════════════════════════════════════════════════════════════
  // CONTEXT
  // ═══════════════════════════════════════════════════════════════════════════

  context {
    analysis {
      underlying_data {
        metric total_orders {
          view order_records {
            label: 'Order records'
            fields: orders_detail
          }

          view user_records {
            label: 'User records'
            fields: users_detail
          }

          view product_records {
            label: 'Product records'
            fields: products_detail
          }
        }

        metric gmv {
          view order_records {
            label: 'Order records'
            fields: orders_detail
          }

          view user_records {
            label: 'User records'
            fields: users_detail
          }
        }

        metric aov {
          view order_value_details {
            label: 'Order value details'
            fields: [
              r(ecommerce_order_items.order_id),
              r(ecommerce_order_items.product_id),
              r(ecommerce_order_items.quantity),
              r(ecommerce_products.price),
            ]
          }

          view aov_by_users {
            label: 'AOV by users'
            fields: users_detail
          }
        }
      }

      breakdown {
        group locations {
          label: 'Locations'
          fields: [
            r(ecommerce_countries.continent_name),
            r(ecommerce_countries.name),
            r(ecommerce_cities.name),
          ]
        }

        group product_categories {
          label: 'Product categories'
          fields: [
            r(map_categories.category),
            r(ecommerce_products.name),
          ]
        }

        group user_demographics {
          label: 'User demographics'
          fields: [
            r(ecommerce_users.gender),
            r(ecommerce_users.age),
          ]
        }
      }
    }
  }
}